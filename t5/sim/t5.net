*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

Vcc vcc 0 10
Vee vee 0 -10
Vin in 0 0 ac 1 sin(0 10m 1k)

X1 pos neg vcc vee out uA741

C1 in pos 220n

R1 pos 0 1k
R2 fin out 500
R3 neg out 100k
R4 neg 0 1k

C2 fin 0 220n

* load

.op
.end

.control

print all

* frequency analysis
ac dec 10 10 1MEG
hardcopy vo1f.ps vdb(fin)
echo vo1f_FIG

*input impedance in kohm
let zi = v(in)[40]/Vin#branch[40]
print zi

*merit
let resp=db(v(fin))
settype decibel resp
let max_gain = vecmax(resp)
let tpoint = max_gain - 3.0
meas ac lowerCOP WHEN vdb(fin)=tpoint
meas ac upperCOP WHEN vdb(fin)=tpoint CROSS=LAST
let bdwidth=upperCOP-lowerCOP
let center = sqrt(lowerCOP*upperCOP)
print center
print max_gain
let merit = 1/(113.44*(40-max_gain)*(center-1000))
print merit

.endc 
